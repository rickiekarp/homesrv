buildscript {
    ext {
        gradleNodeVersion = '1.2.0'
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:$gradleNodeVersion"
    }
}



//------------------------------------------------------------------------------------------------------------------------
//    Plugins - Application
//------------------------------------------------------------------------------------------------------------------------

apply plugin: 'com.moowork.node'

//------------------------------------------------------------------------------------------------------------------------
//    Plugin configuration
//------------------------------------------------------------------------------------------------------------------------

node {
    version = '8.5.0'
    npmVersion = '5.4.2'
    download = true
    workDir = file("${rootDir}/AdminServer/AdminServerFrontend/node")
    nodeModulesDir = file("${rootDir}/AdminServer/AdminServerFrontend")
}



//------------------------------------------------------------------------------------------------------------------------
//    Tasks - NPM
//------------------------------------------------------------------------------------------------------------------------

task cleanFrontend(type: Delete) {
    group 'build client'
    delete "${rootDir}/AdminServer/AdminServerFrontend/dist", "${rootDir}/AdminServer/AdminServerBackend/src/main/resources/static"
}

task cleanNpm(type: Delete) {
    group 'build client'
    dependsOn 'clean'
    delete "${rootDir}/AdminServer/AdminServerFrontend/node", "${rootDir}/AdminServer/AdminServerFrontend/node_modules"
}

task npmUpdate {
    group 'build client'
    dependsOn 'npm_update'
}

//------------------------------------------------------------------------------------------------------------------------
//    Tasks - Standalone client
//------------------------------------------------------------------------------------------------------------------------

task buildStandaloneClient(type: NpmTask, dependsOn: npmInstall) {
    group 'build client'
    description = 'Compile client side folder for development'
    args = ['run', 'buildStandalone']
}

task serveStandaloneClientWatch(type: NpmTask, dependsOn: npmInstall) {
    group 'build client'
    description = "Builds, serves and watches the client side assets for rebuilding"
    args = ['run', 'serveStandaloneWatch']
}

task serveStandaloneClient(type: NpmTask, dependsOn: npmInstall) {
    group 'build client'
    description = "Compile client side folder for production"
    args = ['start']
}

task bootStandaloneClient(type: GradleBuild) {
    group 'application'
    setTasks(['clean', 'buildStandaloneClient', 'serveStandaloneClient'])
//    finalizedBy 'npm_shutdown'
}

task bootStandaloneClientWatch(type: GradleBuild) {
    group 'application'
    setTasks(['clean', 'serveStandaloneClientWatch'])
//    finalizedBy 'npm_shutdown'
}

//------------------------------------------------------------------------------------------------------------------------
//    Tasks - Full Application
//------------------------------------------------------------------------------------------------------------------------

task bootFullApplication(type: GradleBuild) {
    group 'application'
    setTasks([':AdminServerFrontend:npmUpdate', ':AdminServerFrontend:clean', ':AdminServerFrontend:buildClientToSpring', ':AdminServerBackend:bootRun'])
}

//------------------------------------------------------------------------------------------------------------------------
//    Tasks - Integrated client
//------------------------------------------------------------------------------------------------------------------------

task buildClientToSpring(type: NpmTask, dependsOn: npmInstall) {
    group 'build client'
    description = 'Compile client side folder for development'
    args = ['run', 'buildToSpring']
}
